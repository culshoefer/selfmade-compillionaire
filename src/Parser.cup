import java_cup.runtime.*;
import java.util.ArrayList;

scan with {: return lexer.next_token(); :};

parser code {:

  public boolean  debug(){return debug;}
  public void     debug(boolean b){debug = b;}
  public boolean  syntaxErrors;
  private boolean debug = false;

  static class Node {
    public ArrayList<Node> children;
    private static int globalCounter = 0;

    public int counter;
    private Object value;

    public boolean printed = false;
    public boolean childrenPrinted = false;

    public Node(Object value){
      Node.globalCounter += 1;
      this.counter = Node.globalCounter;
      this.value = value;
      this.children = new ArrayList<Node>();
    }

    public void addChild(Node n){ children.add(n); }

    public static Node fromObject(Object o) {
        Node node;
        if(o instanceof Node) node = (Node) o;
        else if(o instanceof String) node = new Node((String) o);
        else node = new Node(o.toString());
        //System.out.println(node);
        return node;
    }

    public static Node n(Object object, Object... children) {
        Node node = Node.fromObject(object);
        for (Object child : children) {
            node.addChild(Node.fromObject(child));
        }
        return node;
    }

    public String toString(){
      String ret = "";
        if (!this.printed) {
            this.printed = true;
            ret += String.format("%d [label=\"%s\"];",
                            counter, value.toString());
        }
        for(Node n: children){
            if (n.childrenPrinted) continue;
            n.childrenPrinted = true;
            if (!ret.equals("")) ret += "\n";
            ret += String.format("%d -> %d;", this.counter, n.counter);
            ret += "\n" + n.toString();
        }
      return ret;
    }
  }

  private Lexer lexer;

  public Parser(Lexer lex) {
    super(lex);
    lexer = lex;
  }

  public void syntax_error(Symbol current_token) {
  report_error(
  "Syntax error at line " + (current_token.left+1) + ", column "
  + current_token.right, null
  );
}

:};

/**
  * TERMINALS
  */

// Questionmark
terminal QUESTIONMARK;
// Identifier
terminal ID;
// Basic data representations
terminal INTEGER, FLOAT, BOOL, CHAR, STRING;
// Boolean
terminal NOT, AND, OR, IMPLIC, EQUAL, NEQUAL;
// If
terminal IF, FI, THEN, ELSE;
// Loops
terminal BREAK, LOOP, POOL;
// Bracket extravaganza
terminal LPAREN, RPAREN; // ()
terminal LCURLY, RCURLY; // {}
terminal LSQUARE, RSQUARE; // []
terminal LANGLE, RANGLE; // <>
// Arithemtic operators
terminal PLUS, MINUS, MULT, DIV, POW;
// Comparions operators
terminal LESS_EQ; // <, <=
// Dictionaries and sequences (collections)
terminal DICT, SEQ, IN; // dict, seq
//Field stuff
terminal FULLSTOP, ALIAS;
// Types
terminal TYPE_BOOL, TYPE_CHAR, TYPE_INT, TYPE_RAT, TYPE_FLOAT, TYPE_TOP;
// Other operators
terminal CONCAT, SEMICOL, COLON, ASSIGN, COMMA, UNDERSCORE; // ::, ;, :, :=, \,, _
// Comments
terminal MULTI_LINE_COMMENT, SINGLE_LINE_COMMENT; // /#...#/, #...

// TODO: Test that function actually returns something of that type (i.e. if func is supposed to have return type int, check there's return int somewhere)
// TODO: Test for allowed/disallowed ASCII characters as string/char AND for variable names
// TODO: Test for capital s in seq, and variations of this
// TODO: Seq length checking
// TODO: Rationals
// TODO: Loop
// TODO: If
// TODO: Break
// TODO: Test Function calls further
// TODO: Don't allow keywords as e.g. function names
// TODO: Null value
// TODO: Alias
// TODO: Are declarations only allowed before main?
// TODO: In function, declarations have to occur first
/**
  * NON-TERMINALS
  */
// functioncall
nonterminal function_call;
// Conditional
nonterminal conditional, compound_statement;
nonterminal element;
// Loops
nonterminal loop_cycle, loop_stmnts, loop_stmnt, loop_conditional;
// Variables and types
nonterminal variable_decl_init, var_decl, type, maybe_type_decl, type_decl, type_def_open, type_def, type_def_assign;
nonterminal zero_or_more_type_def_pairs, no_trailing_colon_type_def_pairs, type_def_element;
nonterminal type_seq, type_dict, collect_type;
// Functions
nonterminal global_declaration_except_main;
// Comments
nonterminal any_comment;
// Parameters
nonterminal top_level_formal_parameter_list, formal_parameter_list;
// Sequence
nonterminal seq_concat, evaluates_to_seq_or_id, seq_definition, no_trailing_comma_expressions, zero_or_more_comma_separated_expressions;
nonterminal slicing, maybe_slicing_simple_exp;
// Dict
nonterminal dict_definition, no_trailing_comma_k_v_pairs, zero_or_more_k_v_pairs, dict_element;
// Boolean
nonterminal bool_expr, bool_oper, in_expr, comparison_expr, comparison_operator;

/**
  * OLD STUFF
  */

// Do something with ILLEGAL_CHARARCTER or drop it.
terminal BADCHAR; //TODO: Return this? When is it used?
terminal PRINT, READ, RETURN, MAIN, FDEF, TDEF;
nonterminal stmnts, stmnt, top_level;
nonterminal full_exp, simple_exp, operator;
nonterminal print_statement, read_statement, return_statement, alias_statement;
nonterminal funcs_or_main, main_func, normal_func, normal_func_body;
nonterminal funcs_or_global_decls, global_declarations, func_param_decl, func_body_no_return, func_body_with_return;

precedence left AND, OR, IMPLIC;
precedence left NOT;
precedence left LANGLE, LESS_EQ, EQUAL, NEQUAL, IN;
precedence left PLUS, MINUS;
precedence left DIV, MULT;
precedence left POW;
precedence left CONCAT;
precedence left FULLSTOP;
precedence left ID; //TODO: Delete this line if necessary, it is a dirty hack
precedence left ELSE;
precedence left QUESTIONMARK;
precedence left LCURLY;
//precedence left RETURN; //TODO: REMOVE THIS IF FIXED WHITESPACE HACK

top_level
::=
    funcs_or_global_decls:fog {: if(debug()) System.out.println((Node)fog); :}
;


funcs_or_global_decls // TODO: Add global variables to this
::=
    funcs_or_main:fs {:RESULT=Node.n(fs);:}
;


//TODO add empty return for main (or any function)
//TODO add error code return for main
main_func
::=
    MAIN func_param_decl:p func_body_no_return:fb {:RESULT=Node.n("MAIN_FUNC", p, fb);:}
  | MAIN func_body_no_return:fb {:RESULT=Node.n("MAIN_FUNC", fb);:}
;

any_comment
::=
    SINGLE_LINE_COMMENT {:RESULT=Node.n("SINGLE_LINE_COMMENT");:}
  | MULTI_LINE_COMMENT {:RESULT=Node.n("MULTI_LINE_COMMENT");:}
;

funcs_or_main
::=
   global_declarations:gd1  main_func:mf global_declarations:gd2 {:RESULT=Node.n("MAIN_THEN_FUNC", gd1, mf, gd2);:};
//  | global_declaration_except_main:gd funcs_or_main:fum {:RESULT=Node.n("DECL_THEN_MAYBE_MAIN", gd, fum);:}
//;

global_declarations
::=
    global_declaration_except_main:f global_declarations:fs {:RESULT=Node.n("FUNC_THEN_FUNCS", f, fs);:}
  | /* nuffin */ {:RESULT=Node.n("FUNCS_END");:}
;

global_declaration_except_main
::=
    any_comment:c {:RESULT=Node.n(c);:}
  | normal_func:f {:RESULT=Node.n(f);:}
  | variable_decl_init:v {:RESULT=Node.n(v);:}
  | type_def:td {:RESULT=Node.n(td);:}
  | alias_statement:as {:RESULT=Node.n(as);:}
  | function_call:fc SEMICOL {:RESULT=Node.n(fc);:}
;

type_def
::=
    type_def_open:tdo SEMICOL {:RESULT=Node.n(tdo);:}
;

type_def_open
::=
    TDEF ID:i LCURLY zero_or_more_type_def_pairs:dd RCURLY {:RESULT=Node.n("TDEF", i, dd);:}
;

zero_or_more_type_def_pairs
::=
    no_trailing_colon_type_def_pairs:kv {: RESULT=Node.n(kv); :}
  | /* nuffin */ {:RESULT=Node.n("EMPTY_TDEF");:}
;

no_trailing_colon_type_def_pairs
::=
     type_def_element:kv COMMA no_trailing_colon_type_def_pairs:kvs {:RESULT=Node.n("NON_ENDING_KV_LIST", kv, kvs);:}
   | type_def_element:kv {:RESULT=Node.n(kv);:}

;

type_def_element
::= simple_exp:k COLON type:v {:RESULT=Node.n("TYPE_DEF_ELEM", k, v);:}
;

normal_func
::=
    FDEF ID:i func_param_decl:p normal_func_body:fb {:RESULT=Node.n("FDEF", i, p, fb);:}
;


func_param_decl
::=
    LPAREN top_level_formal_parameter_list RPAREN {:RESULT=Node.n("PARAM_DECL");:}
;

top_level_formal_parameter_list
::=
    formal_parameter_list:ne {:RESULT=Node.n(ne);:}
   | /* nuffin */ {:RESULT=Node.n("NO_PARAMS");:}
;

formal_parameter_list
::= var_decl:f COMMA formal_parameter_list:fl {:RESULT=Node.n("NON_ENDING_PARAMS_LIST", f, fl);:}
   | var_decl:f {:RESULT=Node.n(f);:}
;

normal_func_body
::=
    func_body_no_return:n {:RESULT=Node.n(n);:}
  | func_body_with_return:r{:RESULT=Node.n(r);:}
;

// TODO: Add declaration between statements
func_body_no_return
::=
    compound_statement:ss SEMICOL {:RESULT=Node.n("FUNC_BODY_NO_RETURN", ss);:}
;

func_body_with_return
::=
    compound_statement:ss type_decl:td SEMICOL {:RESULT=Node.n("FUNC_BODY_WITH_RETURN", ss, td);:}
;

stmnts
::=
    stmnt:s {:RESULT=Node.n(s);:} //TODO: Enforce that declarations come before the other stuff
  | stmnt:s stmnts:ss {:RESULT=Node.n(s, ss);:}
;

stmnt
::=
    variable_decl_init:s {:RESULT=Node.n(s);:}
  | any_comment:c {:RESULT=Node.n(c);:}
  | read_statement:r {:RESULT=Node.n(r);:}
  | print_statement:p {:RESULT=Node.n(p);:}
  | return_statement:re {:RESULT=Node.n(re);:}
  | alias_statement:as {:RESULT=Node.n(as);:}
  | type_def:td {:RESULT=Node.n(td);:}
  | conditional:c {:RESULT=Node.n(c);:}
  | loop_cycle:l {:RESULT=Node.n(l);:}
  | compound_statement:cs {:RESULT=Node.n(cs);:}
  | function_call:fc SEMICOL {:RESULT=Node.n(fc);:}
  | normal_func:nc {:RESULT=Node.n(nc);:}
;

compound_statement
::=
   LCURLY stmnts:ss RCURLY {:RESULT=Node.n("COMPOUND", ss);:}
;

//TODO: Explain in ambiguities.txt that we also allow compound ifs
conditional
::=
    IF LPAREN simple_exp:be RPAREN THEN stmnts:ss FI {:RESULT=Node.n("IF_STATEMENT", be, ss);:}
  | IF LPAREN simple_exp:be RPAREN THEN stmnts:s1 ELSE stmnts:s2 FI {:RESULT=Node.n("IF_ELSE", be, s1, s2);:}
;

/* L O O P B O I S (start) */
loop_cycle
::=
    LOOP loop_stmnts:ss POOL {:RESULT=Node.n("LOOP", ss);:}
;
loop_stmnts
::=
    loop_stmnt:s {:RESULT=Node.n(s);:} //TODO: Enforce that declarations come before the other stuff
  | loop_stmnt:s loop_stmnts:ss {:RESULT=Node.n(s, ss);:}
;
loop_stmnt
::=
    variable_decl_init:s {:RESULT=Node.n(s);:}
  | BREAK {:RESULT=Node.n("BREAK");:}
  | any_comment:c {:RESULT=Node.n(c);:}
  | read_statement:r {:RESULT=Node.n(r);:}
  | print_statement:p {:RESULT=Node.n(p);:}
  | return_statement:re {:RESULT=Node.n(re);:}
  | alias_statement:as {:RESULT=Node.n(as);:}
  | type_def:td {:RESULT=Node.n(td);:}
  | loop_conditional:c {:RESULT=Node.n(c);:}
  | loop_cycle:l {:RESULT=Node.n(l);:}
  | compound_statement:cs {:RESULT=Node.n(cs);:}
;
loop_conditional
::=
    IF LPAREN simple_exp:be RPAREN THEN loop_stmnts:ss FI {:RESULT=Node.n("IF_STATEMENT", be, ss);:}
  | IF LPAREN simple_exp:be RPAREN THEN loop_stmnts:s1 ELSE loop_stmnts:s2 FI {:RESULT=Node.n("IF_ELSE", be, s1, s2);:}
;


/* L O O P B O I S (end) */

alias_statement
::=
    ALIAS type:t ID:i SEMICOL {:RESULT=Node.n("ALIAS", t, i);:}
  | ALIAS type_def_open:t ID:i SEMICOL {:RESULT=Node.n("ALIAS", t, i);:}
;

full_exp
::=
    simple_exp:be SEMICOL {:RESULT=Node.n(be);:}
;

/*simple_exp
::=
    bool_expr:be {:RESULT=Node.n(be);:}
  | simple_exp:se {:RESULT=Node.n(se);:}
;*/

//TODO: Describe syntax of rationals in ambiguities.txt
simple_exp
::=
    INTEGER:i {:RESULT=Node.n(i);:}
  | MINUS INTEGER:i {:RESULT=Node.n(i);:}
  | MINUS FLOAT:i {:RESULT=Node.n(i);:}
  | FLOAT:i {:RESULT=Node.n(i);:}
  | INTEGER:i UNDERSCORE INTEGER:j DIV INTEGER:k {:RESULT=Node.n("MIXED_RAT", i, j, k);:}
  | MINUS INTEGER:i UNDERSCORE INTEGER:j DIV INTEGER:k {:RESULT=Node.n("MIXED_RAT", i, j, k);:}
  | INTEGER:i UNDERSCORE MINUS INTEGER:j DIV INTEGER:k {:RESULT=Node.n("MIXED_RAT", i, j, k);:}
  | INTEGER:i UNDERSCORE INTEGER:j DIV MINUS INTEGER:k {:RESULT=Node.n("MIXED_RAT", i, j, k);:}
  | MINUS INTEGER:i UNDERSCORE MINUS INTEGER:j DIV INTEGER:k {:RESULT=Node.n("MIXED_RAT", i, j, k);:}
  | MINUS INTEGER:i UNDERSCORE INTEGER:j DIV MINUS INTEGER:k {:RESULT=Node.n("MIXED_RAT", i, j, k);:}
  | INTEGER:i UNDERSCORE MINUS INTEGER:j DIV MINUS INTEGER:k {:RESULT=Node.n("MIXED_RAT", i, j, k);:}
  | MINUS INTEGER:i UNDERSCORE MINUS INTEGER:j DIV MINUS INTEGER:k {:RESULT=Node.n("MIXED_RAT", i, j, k);:}
  | CHAR:i {:RESULT=Node.n("CHARVAR");:}
  | STRING:i {:RESULT=Node.n("STRING");:}
  | simple_exp:sd CONCAT simple_exp:sq {:RESULT=Node.n("SEQ_CONCAT", sd, sq);:}
//  | seq_concat:s {:RESULT=Node.n(s);:}
//  | ID:i {:RESULT=Node.n(i);:}
  | element:e {:RESULT=Node.n(e); :}
  | seq_definition:s {:RESULT=Node.n(s); :}
  | dict_definition:d {:RESULT=Node.n(d);:}
  | simple_exp:e1 operator:o simple_exp:e2 {:RESULT=Node.n(o, e1, e2);:}
  | LPAREN simple_exp:e RPAREN {:RESULT=Node.n("PARENS", "(", e, ")");:}
  | function_call:fc {:RESULT=Node.n(fc);:}
  | bool_expr:be {:RESULT=Node.n(be);:} //Let's forget type checking, man
;

function_call
::=
  QUESTIONMARK simple_exp:be QUESTIONMARK ID:i LPAREN zero_or_more_comma_separated_expressions:args RPAREN {:RESULT=Node.n("FUNCTIONCALL", be, i, args);:}
;

bool_expr
::=
   simple_exp:etb1 bool_oper:bo simple_exp:etb2 {:RESULT=Node.n(bo, etb1, etb2);:}
 | NOT simple_exp:be {:RESULT=Node.n("NOT", be);:}
 | in_expr:ie {:RESULT=Node.n(ie);:}
 | BOOL {:RESULT=Node.n("BOOL");:}
  //| LPAREN bool_expr:be RPAREN {:RESULT=Node.n(be);:}
 | comparison_expr:ce {:RESULT=Node.n(ce);:}
;

comparison_expr
::=
   simple_exp:se1 comparison_operator:co simple_exp:se2 {:RESULT=Node.n("Comparison_expr", se1, co, se2);:}
;

comparison_operator
::=
    LANGLE {:RESULT=Node.n("LESS");:}
  | LESS_EQ {:RESULT=Node.n("LESS_EQ");:}
  | EQUAL  {:RESULT=Node.n("=");:}
  | NEQUAL {:RESULT=Node.n("!=");:}
;


//TODO: Reason about precedence of in operator
//TODO: `simple_exp`? Really? Only `ID` can work here. -Tim
in_expr
::=
   simple_exp:se IN ID:i {:RESULT=Node.n(se, "in", i);:}
 | simple_exp:se IN dict_definition:i {:RESULT=Node.n("in", se, i);:}
 | simple_exp:se IN seq_definition:i {:RESULT=Node.n("in", se, i);:}
;

bool_oper
::=
    AND    {:RESULT=Node.n("&&");:}
  | OR     {:RESULT=Node.n("||");:}
  | IMPLIC {:RESULT=Node.n("=>");:}
;

/*seq_concat
::=
//  | evaluates_to_seq_or_id:sd {:RESULT=Node.n(sd);:}
;*/

/*
evaluates_to_seq_or_id
::=
    seq_definition:sd {:RESULT=Node.n(sd);:}
//  | ID:i {:RESULT=Node.n("ID: " + i);:}
  | evaluates_to_seq_or_id:se slicing:sl {://RESULT=Node.n("SLICED_SEQ", se, sl);:}
;*/

slicing
::=
    LSQUARE maybe_slicing_simple_exp:s COLON maybe_slicing_simple_exp:e RSQUARE {:RESULT=Node.n("SLICING", s, e);:}
;

maybe_slicing_simple_exp
::=
    simple_exp:se {:RESULT=Node.n(se);:}
  | /* nuffins */ {:RESULT=Node.n("EMPTY");:}
;

seq_definition
::=
    LSQUARE zero_or_more_comma_separated_expressions:exp RSQUARE {:RESULT=Node.n("SEQ_DEF", exp);:}
;

//TODO: Say in ambiguities.txt that we allow expressions in seq's
zero_or_more_comma_separated_expressions
::=
    no_trailing_comma_expressions:ne {:RESULT=Node.n(ne);:}
   | /* nuffin */ {:RESULT=Node.n("EMPTY_SEQ");:}
;

no_trailing_comma_expressions
::=
     simple_exp:e COMMA no_trailing_comma_expressions:es {:RESULT=Node.n("NON_ENDING_COMMA_LIST", e, es);:}
   | simple_exp:e {:RESULT=Node.n(e);:}
;

dict_definition
::=
   LCURLY zero_or_more_k_v_pairs:kv RCURLY {:RESULT=Node.n("DICT_DEF", kv);:}
;

zero_or_more_k_v_pairs
::=
    no_trailing_comma_k_v_pairs:kv {: RESULT=Node.n(kv); :}
  | /* nuffin */ {:RESULT=Node.n("EMPTY_DICT");:}
;

no_trailing_comma_k_v_pairs
::=
     dict_element:kv COMMA no_trailing_comma_k_v_pairs:kvs {:RESULT=Node.n("NON_ENDING_KV_LIST", kv, kvs);:}
   | dict_element:kv {:RESULT=Node.n(kv);:}

;

dict_element
::= simple_exp:k COLON simple_exp:v {:RESULT=Node.n("DICT_ELEM", k, v);:}
;

read_statement
::=
    READ ID:i SEMICOL {:RESULT=Node.n("READ", i);:}
;

print_statement
::=
    PRINT full_exp:e {:RESULT=Node.n("PRINT", e);:}
;

return_statement
::=
    RETURN full_exp:e {:RESULT = Node.n("RETURN", e);:}
  | RETURN {:RESULT=Node.n("RETURN_HACK");:} //TODO: Fix return, bc right now we allow e.g. return; something;
;

operator
::=
    PLUS  {:RESULT=Node.n("+");:}
  | MINUS {:RESULT=Node.n("-");:}
  | MULT  {:RESULT=Node.n("*");:}
  | DIV   {:RESULT=Node.n("/");:}
  | POW   {:RESULT=Node.n("^");:}
;

variable_decl_init
::=
  type_def_assign:e {:RESULT=Node.n("TYPE_ASSIGNMENT", e);:}
/*    element:e1 ASSIGN full_exp:e2 {:RESULT=Node.n("ASSIGNMENT", e1, e2);:}
  | var_decl:v ASSIGN full_exp:e {:RESULT=Node.n("ASSIGNMENT", v, e);:}*/
  | var_decl:v SEMICOL {:RESULT=Node.n(v);:}
;

type_def_assign
::=
    element:e1 ASSIGN no_trailing_comma_expressions:e2 SEMICOL {:RESULT=Node.n("ASSIGNMENT", e1, e2);:}
  | var_decl:v ASSIGN no_trailing_comma_expressions:e SEMICOL {:RESULT=Node.n("ASSIGNMENT", v, e);:}
;

element
::=
    ID:i {:RESULT=Node.n(i);:}
  | element:b LSQUARE simple_exp:i RSQUARE {:RESULT=Node.n(b, "Index", i);:}
  | element:b slicing:sl {:RESULT=Node.n("SLICED_SEQ", b, sl);:}
  | element:e1 FULLSTOP element:e2 {:RESULT=Node.n("FIELD_REFERENCE", e1, e2);:}
;

var_decl
::=
    ID:i type_decl:td {:RESULT=Node.n("DECLARATION", i ,td);:}
;

type_decl
::=
    COLON type:t {:RESULT=Node.n("TYPE DECLARATION", t);:}
;

type
::=
    TYPE_BOOL   {:RESULT=Node.n("BOOL");:}
  | TYPE_CHAR   {:RESULT=Node.n("CHAR");:}
  | TYPE_INT    {:RESULT=Node.n("INT");:}
  | TYPE_RAT    {:RESULT=Node.n("RAT");:}
  | TYPE_FLOAT  {:RESULT=Node.n("FLOAT");:}
  | type_seq:s  {:RESULT=Node.n(s);:}
  | type_dict:d {:RESULT=Node.n(d);:}
  | ID:i        {:RESULT=Node.n("CUSTOM_TYPE", i);:}
;

collect_type
::=
    type:t   {:RESULT=Node.n(t);:}
  | TYPE_TOP {:RESULT=Node.n("TOP");:}
;

type_seq
::=
    SEQ LANGLE collect_type:t RANGLE {:RESULT=Node.n("SEQ", t);:}
;

type_dict
::=
    DICT LANGLE type:t1 COMMA collect_type:t2 RANGLE {:RESULT = Node.n("DICT", t1, t2);:}
;
